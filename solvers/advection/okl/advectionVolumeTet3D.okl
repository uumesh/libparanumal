/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// thread loop over elements
@kernel void advectionVolumeTet3D(const dlong Nelements,
                                 @restrict const  dfloat *  vgeo,
                                 @restrict const  dfloat *  D,
                                           const  dfloat time,
                                 @restrict const  dfloat *  x,
                                 @restrict const  dfloat *  y,
                                 @restrict const  dfloat *  z,
                                 @restrict const  dfloat *  q,
                                 @restrict dfloat *  rhsq){

for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_F[p_Np];
    @shared dfloat s_G[p_Np];
    @shared dfloat s_H[p_Np];

    for(int n=0;n<p_Np;++n;@inner(0)){

      // prefetch geometric factors (constant on triangle)
      const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
      const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
      const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];
      const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
      const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
      const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];
      const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
      const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
      const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

      // conseved variables
      const dlong  id = e*p_Np + n;
      const dfloat qn  = q[id];

      //  \hat{div} (G*[F;G])
      dfloat cx=0.0, cy=0.0, cz=0.0;
      advectionFlux3D(t, x[id], y[id], z[id], qn, &cx, &cy, &cz);
      s_F[n] = drdx*cx + drdy*cy + drdz*cz;
      s_G[n] = dsdx*cx + dsdy*cy + dsdz*cz;
      s_H[n] = dtdx*cx + dtdy*cy + dtdz*cz;
    }

    for(int n=0;n<p_Np;++n;@inner(0)){

      dfloat rhsqn = 0;
      for(int i=0;i<p_Np;++i){
        const dfloat Drni = D[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = D[n+i*p_Np+1*p_Np*p_Np];
        const dfloat Dtni = D[n+i*p_Np+2*p_Np*p_Np];

        rhsqn += Drni*s_F[i]+Dsni*s_G[i]+Dtni*s_H[i];
      }

      // move to rhs
      const dlong id = e*p_Np + n;
      rhsq[id] = -rhsqn;
    }
  }
}


