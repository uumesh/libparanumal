/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// compute NU = -div(uxu) in collocation way
@kernel void insAdvectionVolumeTet3D(const dlong Nelements,
                                    @restrict const  dfloat *  vgeo,
                                    @restrict const  dfloat *  D,
                                    const dfloat alpha,
                                    const dfloat beta,
                                    @restrict const  dfloat *  U,
                                          @restrict  dfloat *  RHS){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements
    // Hold Flux Vector on Shared, use symmetry f
    @shared dfloat s_F11[p_NblockV][p_Np];
    @shared dfloat s_F12[p_NblockV][p_Np];
    @shared dfloat s_F13[p_NblockV][p_Np];
    @shared dfloat s_F21[p_NblockV][p_Np];
    @shared dfloat s_F22[p_NblockV][p_Np];
    @shared dfloat s_F23[p_NblockV][p_Np];
    @shared dfloat s_F31[p_NblockV][p_Np];
    @shared dfloat s_F32[p_NblockV][p_Np];
    @shared dfloat s_F33[p_NblockV][p_Np];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];

          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];

          const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
          const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
          const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

          const dlong id = e*p_Np*p_NVfields+n;
          const dfloat Un = U[id+0*p_Np];
          const dfloat Vn = U[id+1*p_Np];
          const dfloat Wn = U[id+2*p_Np];

          const dfloat cUn = drdx*Un + drdy*Vn + drdz*Wn;
          const dfloat cVn = dsdx*Un + dsdy*Vn + dsdz*Wn;
          const dfloat cWn = dtdx*Un + dtdy*Vn + dtdz*Wn;

          s_F11[es][n] = cUn*Un;
          s_F12[es][n] = cVn*Un;
          s_F13[es][n] = cWn*Un;

          s_F21[es][n] = cUn*Vn;
          s_F22[es][n] = cVn*Vn;
          s_F23[es][n] = cWn*Vn;

          s_F31[es][n] = cUn*Wn;
          s_F32[es][n] = cVn*Wn;
          s_F33[es][n] = cWn*Wn;
        }
      }
    }

    // Make sure all node data is loaded into @shared

    for(int es=0;es<p_NblockV;++es;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        const dlong e = eo+es;
        if(e<Nelements){
          dfloat nu = 0., nv = 0., nw = 0.;
          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i){
              const dfloat Drni  = D[n+i*p_Np+0*p_Np*p_Np];
              const dfloat Dsni  = D[n+i*p_Np+1*p_Np*p_Np];
              const dfloat Dtni  = D[n+i*p_Np+2*p_Np*p_Np];

              nu += Drni*s_F11[es][i]
                   +Dsni*s_F12[es][i]
                   +Dtni*s_F13[es][i];

              nv += Drni*s_F21[es][i]
                   +Dsni*s_F22[es][i]
                   +Dtni*s_F23[es][i];

              nw += Drni*s_F31[es][i]
                   +Dsni*s_F32[es][i]
                   +Dtni*s_F33[es][i];
            }

          // Update
          const dlong id = e*p_Np*p_NVfields+n;
          if (beta!=0) {
            RHS[id+0*p_Np] = beta*RHS[id+0*p_Np] - alpha*nu;
            RHS[id+1*p_Np] = beta*RHS[id+1*p_Np] - alpha*nv;
            RHS[id+2*p_Np] = beta*RHS[id+2*p_Np] - alpha*nw;
          } else {
            RHS[id+0*p_Np] = -alpha*nu;
            RHS[id+1*p_Np] = -alpha*nv;
            RHS[id+2*p_Np] = -alpha*nw;
          }
        }
      }
    }
  }
}

@kernel void insAdvectionSurfaceTet3D(const dlong Nelements,
                                     @restrict const  dfloat *  sgeo,
                                     @restrict const  dfloat *  LIFT,
                                     @restrict const  dlong  *  vmapM,
                                     @restrict const  dlong  *  vmapP,
                                     @restrict const  int    *  EToB,
                                     const dfloat time,
                                     @restrict const  dfloat *  x,
                                     @restrict const  dfloat *  y,
                                     @restrict const  dfloat *  z,
                                     const dfloat nu,
                                     const dfloat alpha,
                                     @restrict const  dfloat *  U,
                                           @restrict  dfloat *  RHS){
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    // @shared storage for flux terms
    @shared dfloat s_fluxNx[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxNy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxNz[p_NblockS][p_Nfp*p_Nfaces];
    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            const int face = n/p_Nfp;
            // load surface geofactors for this face
            const dlong sid    = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load traces
            const dlong eM = e;
            const dlong eP = idP/p_Np;
            const int vidM = idM%p_Np;
            const int vidP = idP%p_Np;

            const dlong qbaseM = eM*p_Np*p_NVfields + vidM;
            const dlong qbaseP = eP*p_Np*p_NVfields + vidP;

            // load negative and positive trace node values of U, V, Pr
            const dfloat  uM = U[qbaseM+0*p_Np], vM = U[qbaseM+1*p_Np], wM = U[qbaseM+2*p_Np];
                  dfloat  uP = U[qbaseP+0*p_Np], vP = U[qbaseP+1*p_Np], wP = U[qbaseP+2*p_Np];

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              insVelocityDirichletConditions3D(bc, nu, time, x[idM],y[idM],z[idM], nx,ny,nz, uM,vM,wM, &uP,&vP,&wP);
            }

            // Find max normal velocity on the face
            const dfloat unM   = fabs(nx*uM + ny*vM + nz*wM);
            const dfloat unP   = fabs(nx*uP + ny*vP + nz*wP);
            const dfloat unMax = (unM > unP) ? unM : unP;
             // evaluate "flux" terms: LLF
            const dfloat sc = invJ * sJ ;
            s_fluxNx[es][n] = sc*(.5f*( nx*(uP*uP - uM*uM)
                                      + ny*(uP*vP - uM*vM)
                                      + nz*(uP*wP - uM*wM)
                                      + unMax*(uM-uP) ));

            s_fluxNy[es][n] = sc*(.5f*( nx*(vP*uP - vM*uM)
                                      + ny*(vP*vP - vM*vM)
                                      + nz*(vP*wP - vM*wM)
                                      + unMax*(vM-vP) ));

            s_fluxNz[es][n] = sc*(.5f*( nx*(wP*uP - wM*uM)
                                      + ny*(wP*vP - wM*vM)
                                      + nz*(wP*wP - wM*wM)
                                      + unMax*(wM-wP) ));
          }
        }
      }
    }


    // wait for all flux functions are written to @shared

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){
            dfloat rhsnu = 0.0;
            dfloat rhsnv = 0.0;
            dfloat rhsnw = 0.0;
            // Lift
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_Nfaces*p_Nfp;++m){
                const dfloat L = LIFT[n+m*p_Np];
                rhsnu  += L*s_fluxNx[es][m];
                rhsnv  += L*s_fluxNy[es][m];
                rhsnw  += L*s_fluxNz[es][m];
              }

            const dlong id = e*p_Np*p_NVfields + n;
            RHS[id+0*p_Np] -= alpha*rhsnu;
            RHS[id+1*p_Np] -= alpha*rhsnv;
            RHS[id+2*p_Np] -= alpha*rhsnw;
          }
        }
      }
    }
  }
}
