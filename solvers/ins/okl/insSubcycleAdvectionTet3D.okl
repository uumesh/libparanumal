/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// compute NU = -div(uxu) in collocation way
@kernel void insSubcycleAdvectionVolumeTet3D(const dlong Nelements,
                                   @restrict const  dfloat *  vgeo,
                                   @restrict const  dfloat *  D,
                                   @restrict const  dfloat *  Ue,
                                   @restrict const  dfloat *  U,
                                         @restrict  dfloat *  RHS){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements

    // Hold Flux Vector on Shared
    @shared dfloat s_F11[p_NblockV][p_Np];
    @shared dfloat s_F12[p_NblockV][p_Np];
    @shared dfloat s_F13[p_NblockV][p_Np];

    @shared dfloat s_F21[p_NblockV][p_Np];
    @shared dfloat s_F22[p_NblockV][p_Np];
    @shared dfloat s_F23[p_NblockV][p_Np];

    @shared dfloat s_F31[p_NblockV][p_Np];
    @shared dfloat s_F32[p_NblockV][p_Np];
    @shared dfloat s_F33[p_NblockV][p_Np];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){
          const dlong id = e*p_Np*p_NVfields+n;

          // prefetch geometric factors (constant on triangle)
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];

          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];

          const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
          const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
          const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

          const dfloat Un = Ue[id+0*p_Np];
          const dfloat Vn = Ue[id+1*p_Np];
          const dfloat Wn = Ue[id+2*p_Np];

          const dfloat Udn = U[id+0*p_Np];
          const dfloat Vdn = U[id+1*p_Np];
          const dfloat Wdn = U[id+2*p_Np];

          const dfloat cUn = drdx*Un + drdy*Vn + drdz*Wn;
          const dfloat cVn = dsdx*Un + dsdy*Vn + dsdz*Wn;
          const dfloat cWn = dtdx*Un + dtdy*Vn + dtdz*Wn;

          s_F11[es][n] = cUn*Udn;
          s_F12[es][n] = cVn*Udn;
          s_F13[es][n] = cWn*Udn;

          s_F21[es][n] = cUn*Vdn;
          s_F22[es][n] = cVn*Vdn;
          s_F23[es][n] = cWn*Vdn;

          s_F31[es][n] = cUn*Wdn;
          s_F32[es][n] = cVn*Wdn;
          s_F33[es][n] = cWn*Wdn;
        }
      }
    }

    // Make sure all node data is loaded into @shared

    for(int es=0;es<p_NblockV;++es;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        const dlong e = eo+es;
        if(e<Nelements){
          const dlong id = e*p_Np*p_NVfields+n;
          dfloat fluxU = 0., fluxV = 0., fluxW = 0.;

          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i){
              const dfloat Drni = D[n+i*p_Np+0*p_Np*p_Np];
              const dfloat Dsni = D[n+i*p_Np+1*p_Np*p_Np];
              const dfloat Dtni = D[n+i*p_Np+2*p_Np*p_Np];

              fluxU += Drni*s_F11[es][i]
                      +Dsni*s_F12[es][i]
                      +Dtni*s_F13[es][i];

              fluxV += Drni*s_F21[es][i]
                      +Dsni*s_F22[es][i]
                      +Dtni*s_F23[es][i];

              fluxW += Drni*s_F31[es][i]
                      +Dsni*s_F32[es][i]
                      +Dtni*s_F33[es][i];
            }

          RHS[id+0*p_Np] = -fluxU;
          RHS[id+1*p_Np] = -fluxV;
          RHS[id+2*p_Np] = -fluxW;
        }
      }
    }
  }
}

//
@kernel void insSubcycleAdvectionSurfaceTet3D(const dlong Nelements,
                                    @restrict const  dfloat *  sgeo,
                                    @restrict const  dfloat *  LIFT,
                                    @restrict const  dlong  *  vmapM,
                                    @restrict const  dlong  *  vmapP,
                                    @restrict const  int    *  EToB,
                                    const dfloat time,
                                    @restrict const  dfloat *  x,
                                    @restrict const  dfloat *  y,
                                    @restrict const  dfloat *  z,
                                    const dfloat nu,
                                    @restrict const  dfloat *  Ue,
                                    @restrict const  dfloat *  U,
                                          @restrict  dfloat *  RHS){

  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    // @shared storage for flux terms
    @shared dfloat s_fluxU[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxV[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxW[p_NblockS][p_Nfp*p_Nfaces];

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            const int face = n/p_Nfp;
            // load surface geofactors for this face
            const dlong sid    = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load traces
            const dlong eM = e;
            const dlong eP = idP/p_Np;
            const int vidM = idM%p_Np;
            const int vidP = idP%p_Np;

            const dlong qbaseM = eM*p_Np*p_NVfields + vidM;
            const dlong qbaseP = eP*p_Np*p_NVfields + vidP;

            // load negative and positive trace node values of Ux, Uy, Pr
            const dfloat UM = Ue[qbaseM+0*p_Np];
            const dfloat VM = Ue[qbaseM+1*p_Np];
            const dfloat WM = Ue[qbaseM+2*p_Np];
            const dfloat UP = Ue[qbaseP+0*p_Np];
            const dfloat VP = Ue[qbaseP+1*p_Np];
            const dfloat WP = Ue[qbaseP+2*p_Np];

            const dfloat UdM = U[qbaseM+0*p_Np];
            const dfloat VdM = U[qbaseM+1*p_Np];
            const dfloat WdM = U[qbaseM+2*p_Np];
                  dfloat UdP = U[qbaseP+0*p_Np];
                  dfloat VdP = U[qbaseP+1*p_Np];
                  dfloat WdP = U[qbaseP+2*p_Np];

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0) {
              insVelocityDirichletConditions3D(bc, nu, time, x[idM], y[idM], z[idM], nx, ny, nz, UdM, VdM, WdM, &UdP, &VdP, &WdP);
            }

            // Find max normal velocity on the face
            const dfloat unM   = fabs(nx*UM + ny*VM + nz*WM);
            const dfloat unP   = fabs(nx*UP + ny*VP + nz*WP);
            const dfloat unMax = (unM > unP) ? unM : unP;

            // evaluate "flux" terms: LLF
            const dfloat sc = invJ * sJ ;
            s_fluxU[es][n] = sc*(.5f*( nx*(UP*UdP - UM*UdM)
                                     + ny*(VP*UdP - VM*UdM)
                                     + nz*(WP*UdP - WM*UdM)
                                     + unMax*(UdM-UdP) ));

            s_fluxV[es][n] = sc*(.5f*( nx*(UP*VdP - UM*VdM)
                                     + ny*(VP*VdP - VM*VdM)
                                     + nz*(WP*VdP - WM*VdM)
                                     + unMax*(VdM-VdP) ));

            s_fluxW[es][n] = sc*(.5f*( nx*(UP*WdP - UM*WdM)
                                     + ny*(VP*WdP - VM*WdM)
                                     + nz*(WP*WdP - WM*WdM)
                                     + unMax*(WdM-WdP) ));
          }
        }
      }
    }

    // wait for all flux functions are written to @shared

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){

            dfloat rhsu = 0.0;
            dfloat rhsv = 0.0;
            dfloat rhsw = 0.0;
            // Lift
            #pragma unroll p_NfacesNfp
            for(int m=0;m<p_Nfaces*p_Nfp;++m){
              const dfloat L = LIFT[n+m*p_Np];
              rhsu  += L*s_fluxU[es][m];
              rhsv  += L*s_fluxV[es][m];
              rhsw  += L*s_fluxW[es][m];
             }
            // M^-1* (div(u*u)) + Lift*(F*-F))
            const dlong id = e*p_Np*p_NVfields + n;
            RHS[id+0*p_Np] -= rhsu;
            RHS[id+1*p_Np] -= rhsv;
            RHS[id+2*p_Np] -= rhsw;
          }
        }
      }
    }
  }
}
